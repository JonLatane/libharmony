buildscript {
    ext.kotlin_version = '1.0.7'
    ext.kotlin_js_version = '1.0.7'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin2js'

repositories {
    maven {
        url 'https://dl.bintray.com/kotlin/kotlin-dev/'
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-js-library:$kotlin_js_version"
}

compileKotlin2Js {
    kotlinOptions {
        sourceMap = false
        outputFile = "${rootProject.projectDir}/dist/web/libharmony-${version}.js"
        suppressWarnings = false
        verbose = true
        metaInfo = true
    }
    doLast {
        configurations.compile.each { File file ->
            copy {
                includeEmptyDirs = false

                from zipTree(file.absolutePath)
                into "${rootProject.projectDir}/dist/web"
                include { fileTreeElement ->
                    def path = fileTreeElement.path
                    path.endsWith('.js') && (path.startsWith('META-INF/resources/') || !path.startsWith('META-INF/'))
                }
            }
        }
    }
}

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    main.kotlin.srcDirs += '../shared/src/main/kotlin'
}